{ cons, length, slice, empty, head, tail, map, join, foldr, foldl, unfoldr } where (

arith = #"arith.cru",
logic = #"logic.cru",

if = logic.if,
eq = arith.eq,
add = arith.add,
sub = arith.sub,

cons a b = [a, ...b],
length = __builtin_length,
slice = __builtin_slice,
head l = l.[0],
tail l = slice l 1 (length l),
empty l = eq (length l) 0,

list s a b = if (empty s) b (a (head s) (tail s)),

foldr g b s =
  let
    N = length s,
    f n =
      if (eq n N) b $
      g s.[n] (f (add n 1)) in
  f 0,

foldl g b s =
  let
    N = length s,
    f n =
      if (eq n -1) b $
      g s.[n] (f (sub n 1)) in
  f (sub N 1),

map f = foldr (\a b -> [f a, ...b]) [],

unfoldr f =
  let g l b =
    let e = f b in
    if (eq e undefined) l $
    g [...l, e.[0]] e.[1] in
  g [],

join c l =
  if (empty l) "" $
  add (head l) $ foldr nonempty "" (tail l)
  where (
    nonempty a b = add c $ add a b)

)
